ScriptName MyFuncs
## functions which can be called from a macro or a function.

####################################
Function CycleTargetDefocus 3 0 TD_low TD_high step
# function to cycle to new target defocus, within a range and with a step
# calling example:
# CallFunction MyFuncs::CycleTargetDefocus -1.2 -2.2 0.2

#Call Parameters

Echo ===> Running CycleTargetDefocus ...
Echo >>>> defined Range and Step (um)  => [ $TD_low, $TD_high ], [ $step ].

## no editing below ##
delta = -1 * $step

# if the current TargetDefocus is outside of range, make it as TD_low.
# Otherwise, increase a step of $delta

SuppressReports
ReportTargetDefocus 
If $repVal1 > $TD_low OR $repVal1 < $TD_high
   SetTargetDefocus $TD_low
Else 
   IncTargetDefocus $delta
   ChangeFocus $delta			# this might save a few seconds
Endif

ReportTargetDefocus TD
TD = ROUND $TD 2
Echo TargetDefocus = $TD um
EndFunction 

################################################
Function CropImageAToBuffer 0 1 buffer
# calling example:
# CallFunction MyFuncs::CropImageAToBuffer T
#=============================================
Echo ===> Running CropImageAToBuffer ...

#SupressReports

## get info for A and $buffer
ImageProperties $buffer
XP = $reportedValue1
YP = $reportedValue2

ImageProperties A
XA = $reportedValue1
YA = $reportedValue2

Xdiff = $XA - $XP
Ydiff = $YA - $YP

half_Xdiff = $Xdiff / 2
half_Ydiff = $Ydiff / 2

X0 = $half_Xdiff 
X1 = $XP + $half_Xdiff

Y0 = $half_Ydiff 
Y1 = $YP + $half_Ydiff

# crop
CropImage A $X0 $X1 $Y0 $Y1
EndFunction 

####################################################
Function CropImage 1 1 frac buffer 
## crop image in A buffer to quarter size
## calling example
## CallFunction MyFuncs::CropImage 0.25 P

ImageProperties $buffer
X = $repVal1
Y = $repVal2

halfX = $X / 2
halfY = $Y / 2
QX = $X * $frac
QY = $Y * $frac
X0 = $halfX - ( $QX / 2 )
X1 = $halfX + ( $QX / 2 )
Y0 = $halfY - ( $QY / 2 )
Y1 = $halfY + ( $QY / 2 ) - 1

# crop
CropImage $buffer $X0 $X1 $Y0 $Y1
EndFunction

####################################################
Function CropImageCenter 2 1 pixX pixY buffer 
## crop image from center in a buffer to x y (pixle) size
## calling example:
## CallFunction MyFuncs::CropImageCenter 100 100 P

ImageProperties $buffer
X = $repVal1
Y = $repVal2

halfX = $X / 2
halfY = $Y / 2
QX = $pixX
QY = $pixY
X0 = $halfX - ( $QX / 2 )
X1 = $halfX + ( $QX / 2 )
Y0 = $halfY - ( $QY / 2 )
Y1 = $halfY + ( $QY / 2 ) - 1

# crop
CropImage $buffer $X0 $X1 $Y0 $Y1
EndFunction

####################################################
Function AlignToBuffer 1 1 iter buffer 
# aligning how many times to a buffer 
# calling example
# CallFunction MyFuncs::AlignToBuffer 2 T
#========================================
# *** Note: Align to buffer T, twice ****

SuppressReports

## Low Dose
ReportLowDose 
If $reportedValue1 == 1
    ImageLowDoseSet $buffer
    S = $reportedValue1
Elseif $reportedValue1 == 0
    ImageProperties $buffer
    S = $reportedValue6
Endif

## get image parameters in buffer $buffer
ImageProperties $buffer
XP = $reportedValue1
YP = $reportedValue2
bin = $reportedValue3
E = $reportedValue4
# PixelSize = $reportedValue5
#S = $reportedValue6

# figure out from which set the image was taken
#(0=view, 1=focus, 2=trial, 3=record, 4=preview)#
If $S == 0
    shot = View
Elseif $S == 1
    shot = Focus
Elseif $S == 2 
    shot = Trial
Elseif $S == 3
    shot = Record
Elseif $S == 4
    shot = Preview
Elseif $S == 5 
    shot = Search
Endif

## set camera parameters
SetExposure $S $E
SetCenteredSize $S $bin $XP $YP

## align
Loop $iter ind
    $shot
    # still need crop, for Camera which doesn't do flexible sub-size like FEI cameras
    ImageProperties A
    XA = $repVal1
    YA = $repVal2
    If $XA > $XP OR $YA > $YP
        echo CallFunction  MyFuncc::CropImageAToBuffer $buffer
        CallFunction  MyFuncs::CropImageAToBuffer $buffer
    Endif
    AlignTo $buffer
    If $ind == $iter  	# last round of loop, relax stage
    	ResetImageShift 2
    Else 
    	ResetImageShift
    Endif
EndLoop 

## reset camera
RestoreCameraSet
EndFunction 

######################################################
Function ZeroIS-quick 2 0 limit dead
# two arguments are -  shift threshold and dead shift (too large)

# 2014-10-21 12:05:23

Echo ===> Running ZeroIS-quick ...

#- no editing below -#

# IS from Realign routine
ReportImageShift X Y
# not sure above will work or not
#X = $repVal1
#Y = $repVal2

IS = sqrt $X * $X + $Y * $Y

If $IS <= $limit
    ClearAlignment
    echo IS ($IS) <= limit ($limit) um
    echo Clearalignment
Elseif $IS > $limit AND $IS <= $dead
    echo IS ($IS) > limit ($limit) um
    echo Resetimageshift with Relaxing stage ...
    ResetImageShift 2
Else 
    Echo IS ($IS) > $dead um, skip this point!
Endif
EndFunction 

###################################################
Function Drift 1 1 crit shot
# A function to measure drift rate, if good, skip to the end of loop. 
# Otherwise, exit execution -- i.e. skip the point. 
# calling example:
# CallFunction MyFuncs::Drift 1 F
Echo ===> Running Drift $argVal1(A)...

#=============
# parameters 
#=============
## drift rate threshold
# crit = $argVal1
# set these above from centralized macro - Parameters 
# shot = F
interval = 15
times = 10

period = $interval + 1
#SuppressReports
#ResetClock 

$shot
Delay $interval
Loop $times index
$shot
AlignTo B
ReportAlignShift
ClearAlignment
dx = $reportedValue3
dy = $reportedValue4
dist = sqrt $dx * $dx + $dy * $dy
rate = $dist / $period * 10	
#ReportClock
echo Rate = $rate A/sec
echo ----------------

If $rate < $crit
    echo Drift is low enough after shot $index      
    break
Elseif  $index < $times
    Delay $interval
Else
    echo Drift never got below $crit: Skipping ...
    exit   
Endif
EndLoop
EndFunction

######################################################
Function CalEucentricFocus 0 0
# macro to calibrate EucentriFocus using F 
# update @ 2014-10-22 07:55:56

Echo ===> Running CalEucFocus ...
Echo *** Assuming specimen is already at Eucentricitt ***

# use SEM built-in wobbling way to get to Eucentricity
#Eucentricity 3

## record original TargetDefocus
ReportTargetDefocus 
orig = $repVal1

## make sure TargetDefocus is at 0
SetTargetDefocus 0

## bring Objective to eucentric
## not to normalize because beam will be 
## drifting afterwards.

#NormalizeLenses 2
#Delay 1
Loop 3
   G
EndLoop

## Cal - record the value in memory, unless in admin mode
## and save it to calibration file.
CalEucentricFocus

## restore original target defocus
SetTargetDefocus $orig
EndFunction

####################################################################
Function CalEucentricFocus_View 0 0
# macro to quickly calbrate EucentricFocus value using View in LD. 
# update @ 2014-10-21 12:28:58

Echo ===> Running CalEucFocus_View ...
Echo *** Assuming specimen is already at Eucentricity ***

# use SEM built-in wobbling way to get to Eucentricity
#Eucentricity 3

## record original TargetDefocus
ReportTargetDefocus 
orig = $repVal1

## make sure TargetDefocus is at 0
SetTargetDefocus 0

## take care of LD offset
GoToLowDoseArea V
SaveFocus 
SetStandardFocus 0

## Bring objective to Eucentric Focus. Not use normalize
## because it caused beam to shift (not stable) 
#NormalizeLenses 2
#Delay 1
Loop 3
# use View in LD
	G 1 1
EndLoop
CalEucentricFocus

## Restore Focus
RestoreFocus 

## restore original target defocus
SetTargetDefocus $orig
EndFunction

###############################################################
Function Z_byG 0 0
#
# a function to adjust the eccentric center using beam tilted pairs.
# It uses Autofocus to measure the focus and adjust Z instead.
#

Echo ===> Running Z_byG ...

# If in Low Dose, the area should be at Focus first
# before defocus is zeroed, because it could come from V with 
# large defocus offset. It could float up in that case. 

SuppressReports
ReportLowDose
# if in LowDose and area is V
If $reportedValue1 == 1 AND $reportedValue2 == 0
  GoToLowDoseArea F
Endif 

#==================
# set objective lens 
#==================
SetEucentricFocus
#NormalizeLenses 2
#Delay 1

#===========
# Adjust Z
#===========
Loop 2
Autofocus -1
ReportAutofocus DEF
Z = -1 * $DEF
#t = -1 * $reportedValue1
MoveStage 0 0 $Z
Z = ROUND $Z 2
echo --> Z moved $DEF micron 
EndLoop
EndFunction

#########################################################
Function Z_byV 0 0

Echo ===> Running Z_byV ...
#====================================
# for defocus offset of V in Low Dose, save it
# ===================================
GoToLowDoseArea V
SaveFocus

#==================
# set object lens 
#==================
SetEucentricFocus
#NormalizeLenses 2
#Delay 1

#===========
# Adjust Z
#===========
Loop 2
Autofocus -1 1
ReportAutofocus DEF
Z = -1 * $DEF
#Z = -1 * $repVal1
MoveStage 0 0 $Z
Z = ROUND $Z 2
echo Z has moved --> $Z micron 
EndLoop

#=========================================
# restore the defocus set in V originally
# ========================================
RestoreFocus
EndFunction

#######################################################
Function DistanceFromLastItem 0 0 
# a function to return distance how far stage moved from last A point

ReportNavItem 
If $navAcqIndex == 1
   echo -> This is the first Acq Item
   DIS = 0
Else
   X_curr = $repVal2
   Y_curr = $repVal3
   DIS = SQRT (($X_curr - $X ) * ($X_curr - $X) + ($Y_curr - $Y) * ($Y_curr - $Y)) 
Endif 
echo => variable DIS has value $DIS

#clear for this round, carry over to next item
ClearPersistentVars 
X := $X_curr
Y := $Y_curr

EndFunction 

###########################################################
Function ToggleScreen 0 0
ReportScreen 

If $repVal1 = 0
   ScreenDown
Else
   ScreenUp
Endif
EndFunction 

############################################################ 
Function RecordWithIS 2 0 radius number
# take shots using Image Shift after hole is centered
# for 2 um hole, maybe radius should use 0.75 when call this function like
# beam size set as 1um, take 6 shot around hole edge
#
# CallFunction MyFuncs::RecordWithIS 0.75 6
#

ang_base = 360 / $number
Loop $number index
  ang = ( $index - 1 ) * $ang_base
  X = $radius * COS $ang
  Y = $radius * SIN $ang

# now take shot
  ImageShiftByMicrons $X $Y
  Record
  SetImageShift 0 0
EndLoop

###############################################################
Function LMMCars 0 0 

Loop $#cat index
ResetClock
LoadCartridge $cat[$index]
SetNavRegistration $cat[$index]
SetColumnOrGunValve 1
MoveStageTo 0 0 
OpenNewMontage 0 0 LMM-Car$cat[$index]-$name[$index].st
Montage 
NewMap
CloseFile
# make a jpg file for map overview
CallFunction MyFuncs::MapToJPG
ReportClock T
echo $T
echo
EndLoop 

EndFunction 

##################################################################

Function AmongList? 1 0 nP

#list = { 1 4 5 9 }
#echo  $#list

n = $nP


Loop $#list ind
if $n == $list[$ind]
 last = $ind
  break
else
   last = $ind
Endif
EndLoop

If $last <  $#list  OR $n ==  $list[$#list] 
   #echo Among
   RepVal1 = 1
Else
  # echo Not among
   RepVal1 = 0
Endif

EndFunction

#################################################################
Function IsFrameMdocOpen 0 0 
# function to output reminding message on log widow if frame mdoc file is NOT open. 

ReportFrameMdocOpen
if $RepVal1 == 0
   echo >>>>>>>>>> Frame Mdoc file NOT Open <<<<<<<<<<<<<<<<
Endif 
EndFunction

##############################################
Function Z_byV2 1 0 offset
Echo ===> Running Z_byV2 ...
#====================================
# for defocus offset of V in Low Dose, save it
# ===================================
GoToLowDoseArea V
SaveFocus

#==================
# set object lens 
#==================
SetEucentricFocus
ChangeFocus $offset                         # for -300um offset 

#===========
# Adjust Z
#===========
Loop 2
Autofocus -1 2
ReportAutofocus 
Z = -1 * $repVal1
MoveStage 0 0 $Z
Z = ROUND $Z 2
echo Z has moved --> $Z micron 
EndLoop

#=========================================
# restore the defocus set in V originally
# ========================================
RestoreFocus
EndFunction

##################################################
Function AlignZLPFromDM 0 0
# using AutoIT script to align ZLP once a while 

# get out from Low Dose mode first, or beam is blanked
ReportLowDose
if $repVal1 == 1
   SetLowDoseMode 0
endif 

# raise the screen, doesn't hurt even it is already up
ScreenUp

# now do it from Autoit script AlignZLP
Pause Align ZLP From DM

# when done, turn on Low Dose mode 
SetLowDoseMode 1

#report time 
Echo Align ZLP from DM interface - Done! 
ProgramTimeStamps
EndFunction

##################################################
Function MapToJPG 0 0
# 
# SerialEM Script to convert map overview to a jpg image. 
# it works on currently selected map item.
# 
# Chen Xu <chen.xu@umassmed.edu>
# Created: 2018-04-27
# Updated: 2018-04-27
#

# skip non-map item
ReportNavItem
If $RepVal5 != 2        # if not a map item
  Echo -> Not a map item
  Exit
EndIf

# load map overview into Q unbinned
SetUserSetting BufferToReadInto 16	# Q is 16th in alphabet, if A is 0.
SetUserSetting LoadMapsUnbinned 1   
LoadNavMap

# make a jpeg image
ReduceImage Q 2         # assuming loading buffer is Q, and reduce 2 to make JPG image density range more pleasant
SaveToOtherFile A JPG JPG $navNote.jpg
EndFunction

###################################################################
Function NewMapToJPG 0 0
# 
# SerialEM Script to convert last item - map overview to a jpg image. 
# It uses Note string as part of jpg filename.
# it works on an item which creates a map and should work for "Acquire at points..."
# as "Run Script after". 
# 
# Chen Xu <chen.xu@umassmed.edu>
# Created: 2018-04-27
# Updated: 2018-04-30
#

# skip non-map item
ReportOtherItem -1
If $RepVal5 != 2        # if not a map item
  Echo -> Not a map item, exit ...
  Exit
EndIf

# load map overview into Q unbinned
SetUserSetting BufferToReadInto 16	# Q is 16th in alphabet, if A is 0.
SetUserSetting LoadMapsUnbinned 1   
LoadOtherMap -1				# last item on the nav list

# make a jpeg image
ReduceImage Q 2         # assuming loading buffer is Q, and reduce 2 to make JPG image density range more pleasant
SaveToOtherFile A JPG JPG $navNote.jpg
EndFunction

##################################################
Function AToJPG 0 0
# 
# SerialEM Script to convert save image in buffer A to a jpg image. 
# it reduces image in A by 2 for comfortable JPG density range. It 
# takes current filename and Z into jpg filename. Therefore, MRC file
# is required to be opened.
# 
# Chen Xu <chen.xu@umassmed.edu>
# Created: 2018-04-29
# Updated: 2018-04-29
#

ReportCurrentFilename 1
root = $RepVal1 
ext = $RepVal2
ReportFileZsize
z = $RepVal1

ReduceImage A 2
SaveToOtherFile A JPG JPG $root-$z.jpg
EndFunction
